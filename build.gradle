plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.imd'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    /**
     * Spring Core dependencies
     *
     * The version of org.springframework.boot is declared at plugins block that placed top of this script.
     */
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    /**
     * Database dependencies
     */
    // It would be used until the database setup is done.
    runtimeOnly 'com.h2database:h2'
    // The version of the connector will be fixed when the database setup is done.
    runtimeOnly 'org.postgresql:postgresql'
    /**
     * Test dependencies
     */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    /**
     * Tools
     */
    /*Lombok*/
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.18'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.18'

    /*swagger*/
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.3'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-data-rest', version: '1.5.3'

    /*devtools*/
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'dev' : profile

sourceSets {
    main {
        java {
        }
        resources {
            srcDirs += ["src/main/resources-${profile}"]
        }
    }
}

test {
    useJUnitPlatform()
}

task buildProd(type: Exec) {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        commandLine "gradlew.bat"
    } else {
        commandLine "./gradlew"
    }
    args "clean", "build", "-Pprofile=prod"
}
